apply plugin: 'com.android.application'

ext {
    mockitoConfigFile = file("${project.rootDir}/app/src/test/resources/mockito-extensions/org.mockito.plugins.MockMaker")
    mockitoExperimentalFinalMockingEnabled = true
}

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"
    defaultConfig {
        applicationId "cz.sveda.mockito_final_mocking_demo"
        minSdkVersion 21
        targetSdkVersion 24
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions.unitTests.all {
        mockitoExperimentalFinalMockingEnabled ? enableMockingOfFinalClasses() : disableMockingOfFinalClasses()
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:24.2.1'
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.2.15'
    testCompile 'pl.pragmatists:JUnitParams:1.0.5'
}

def enableMockingOfFinalClasses() {
    ext.mockitoConfigFile.getParentFile().mkdirs()
    def writer = new FileWriter(mockitoConfigFile)
    try {
        writer.write('mock-maker-inline')
        writer.flush()
    } finally {
        writer.close()
    }
}

def disableMockingOfFinalClasses() {
    ext.mockitoConfigFile.delete()
}
