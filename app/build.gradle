apply plugin: 'com.android.application'

ext {
    mockitoConfigFile = file("${project.rootDir}/app/src/test/resources/mockito-extensions/org.mockito.plugins.MockMaker")
    mockitoExperimentalFinalMockingEnabled = System.getProperty('mockitoExperimentalFinalMockingEnabled', "true").toBoolean()
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "cz.sveda.mockito_final_mocking_demo"
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions.unitTests.all {
        mockitoExperimentalFinalMockingEnabled ? enableMockingOfFinalClasses() : disableMockingOfFinalClasses()
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.2.15'
    testCompile 'pl.pragmatists:JUnitParams:1.0.5'
}

def enableMockingOfFinalClasses() {
    ext.mockitoConfigFile.getParentFile().mkdirs()
    def writer = new FileWriter(mockitoConfigFile)
    try {
        writer.write('mock-maker-inline')
        writer.flush()
    } finally {
        writer.close()
    }
}

def disableMockingOfFinalClasses() {
    ext.mockitoConfigFile.delete()
}
